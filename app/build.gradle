apply plugin: 'com.android.application'
apply from: 'wayland-scanner.gradle'

def waylandOut = project.buildDir.absolutePath + "/wayland"

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "com.termux.x11"
        minSdkVersion 24

        /*
         * Do not increase targetSdkVersion
         * Android does not allow to manage socket
         * owned by other userId if targetSdkVersion is 29+
         */
        //noinspection ExpiredTargetSdkVersion,OldTargetApi
        targetSdkVersion 28
        versionCode 10
        versionName "1.02.06"

        externalNativeBuild {
            cmake {
                arguments "-DWAYLAND_GENERATED_DIR=${waylandOut}"
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file('testkey_untrusted.jks')
            keyAlias 'alias'
            storePassword 'xrj45yWGLbsO7W0v'
            keyPassword 'xrj45yWGLbsO7W0v'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    packagingOptions {
        jniLibs {
            // This will allow us to use shared libraries inside *.apk, without unpacking
            // @agnostic-apollo is the best
            // https://github.com/termux/termux-x11/commit/6cdfb75c4451eef63f114a93baef5ba73b7cfd8c#commitcomment-77856313
            useLegacyPackaging false
        }
    }
}

task patchWayland {
    println("Patch can fail here, it is normal if a patch was already applied")
    ant.patch(patchfile: "${project.projectDir}/src/main/cpp/wayland.patch", dir: "${project.projectDir}/src/main/cpp/wayland/src", failonerror:false)
    ant.patch(patchfile: "${project.projectDir}/src/main/cpp/wayland2.patch", dir: "${project.projectDir}/src/main/cpp/wayland", failonerror:false)
}

tasks.matching { it.name.toLowerCase().contains("ndk") && !it.name.toLowerCase().contains("clean") }
        .all { it.dependsOn(patchWayland) }

wayland {
    out waylandOut
    scan "protocol/wayland.xml"
    scan "protocol/xdg-shell.xml", "wayland"
}

dependencies {
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
    implementation 'com.termux.termux-app:termux-shared:-SNAPSHOT'
    implementation 'com.google.android.material:material:1.8.0'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //noinspection GradleDependency
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'androidx.annotation:annotation:1.5.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    compileOnly project(':shell-loader:stub')
}
