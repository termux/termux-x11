//file:noinspection UnnecessaryQualifiedReference, GrDeprecatedAPIUsage

// Detect android studio sync and disable generating run/debug configuration for shell-loader
if (gradle.startParameter.taskNames.isEmpty())
    apply plugin: 'com.android.library'
else
    apply plugin: 'com.android.application'

android.namespace 'com.termux.x11.shell_loader'
android.defaultConfig.minSdkVersion 26
android.compileSdkVersion 34
android.buildTypes.debug.signingConfig null
android.buildFeatures.buildConfig true
android.compileOptions.sourceCompatibility JavaVersion.VERSION_1_8
android.compileOptions.targetCompatibility JavaVersion.VERSION_1_8
dependencies.compileOnly project(':shell-loader:stub')

if (!gradle.startParameter.taskNames.isEmpty())
    android.applicationVariants.configureEach { outputs.configureEach { outputFileName = "shell-loader-debug.apk" } }

def signingConfig = project(':app').android.signingConfigs.debug
def keyStore = java.security.KeyStore.getInstance(java.security.KeyStore.getDefaultType())
keyStore.load(new FileInputStream(signingConfig.storeFile), signingConfig.keyPassword.toCharArray())

android.defaultConfig.buildConfigField "String", "APPLICATION_ID", "\"com.termux.x11\""
android.defaultConfig.buildConfigField "String", "logTag", "\"Termux:X11 loader\""
android.defaultConfig.buildConfigField "int", "SIGNATURE", String.valueOf(Arrays.hashCode(keyStore.getCertificate(signingConfig.keyAlias).getEncoded()))
android.defaultConfig.buildConfigField "String", "CLASS_ID", "\"com.termux.x11.CmdEntryPoint\""
android.defaultConfig.buildConfigField "String", "packageNotInstalledErrorText",
        """\"Termux:X11 application is not found.\\n\" +
        \"You should choose latest release here: \\n https://github.com/termux/termux-x11/releases/tag/nightly\\n\" +
        \"After this you should download \\"termux-x11-ARCH-debug.apk\\" or \\"termux-x11-universal-debug.apk\\" and install it.\\n\""""
android.defaultConfig.buildConfigField "String", "packageSignatureMismatchErrorText",
        "\"Signature verification of target application " + android.namespace + " failed.\\nPlease, reinstall both termux-x11 package and Termux:X11 application from the same source\""
android.defaultConfig.buildConfigField "String", "COMMIT", "\"" + ("git rev-parse HEAD\n".execute().getText().trim() ?: (System.getenv('CURRENT_COMMIT') ?: "NO_COMMIT")) + "\""
